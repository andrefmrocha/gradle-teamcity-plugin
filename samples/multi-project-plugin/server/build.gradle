
apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'com.github.rodm.teamcity-environments'

ext {
    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java7Home = project.findProperty('java7.home') ?: '/opt/jdk1.7.0_80'
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
}

dependencies {
    implementation project(':common')
    agent project(path: ':agent', configuration: 'plugin')
}

teamcity {
    version = teamcityVersion

    server {
        descriptor {
            name = 'Example TeamCity Plugin'
            displayName = 'Example TeamCity Plugin'
            version = project.version
            vendorName = 'rodm'
            vendorUrl = 'http://example.com'
            description = 'Example multi-project TeamCity plugin'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        'teamcity2019.1' {
            version = '2019.1.5'
            javaHome = file(java8Home)
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }

        'teamcity2020.1' {
            version = '2020.1'
            javaHome = file(java8Home)
        }
    }
}
