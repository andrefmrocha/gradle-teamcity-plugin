
plugins {
    id 'com.github.rodm.teamcity-environments' version '1.4-rc-1'
}

group = 'com.github.rodm.teamcity'
version = '1.0-SNAPSHOT'

ext {
    vendorName = 'rodm'
    teamcityVersion = '2020.1'

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java7Home = project.findProperty('java7.home') ?: '/opt/jdk1.7.0_80'
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_121'
}

configurations {
    teamcityPlugins
}

dependencies {
    teamcityPlugins project(path: ':plugin1', configuration: 'plugin')
    teamcityPlugins project(path: ':plugin2', configuration: 'plugin')
}

teamcity {
    version = teamcityVersion

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        'teamcity2020.1' {
            version = '2020.1.5'
            javaHome = file(java8Home)
            plugins = configurations.teamcityPlugins
        }

        'teamcity2021.1' {
            version = '2021.1.1'
            javaHome = file(java8Home)
            plugins = configurations.teamcityPlugins
        }
    }
}
