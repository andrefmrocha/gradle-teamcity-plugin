
plugins {
    id 'org.gradle.groovy'
    id 'org.gradle.java-gradle-plugin'
    id 'org.gradle.jacoco'
    id 'org.gradle.maven-publish'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.sonarqube' version '2.8'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'de.undercouch:gradle-download-task:4.0.4'
    implementation 'org.jetbrains.intellij.plugins:structure-base:3.112'
    implementation 'org.jetbrains.intellij.plugins:structure-teamcity:3.112'
    implementation 'org.jetbrains.intellij:plugin-repository-rest-client:2.0.11'

    testImplementation(platform('org.junit:junit-bom:5.6.2')) {
        because('Platform, Jupiter, and Vintage versions should match')
    }
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.xmlunit:xmlunit-matchers:2.7.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.mockito:mockito-core:3.3.3'

    testRuntimeOnly ('org.junit.vintage:junit-vintage-engine') {
        because('allows JUnit 3 and JUnit 4 tests to run')
    }
}

version = '1.3.2'
group = 'com.github.rodm'

sourceSets {
    functionalTest {
        compileClasspath += main.output + configurations.testRuntimeClasspath
        runtimeClasspath += main.output + configurations.testRuntimeClasspath
        groovy {
            srcDir 'src/functional/groovy'
        }
    }
    samples {
        compileClasspath += main.output + configurations.testRuntimeClasspath
        runtimeClasspath += main.output + configurations.testRuntimeClasspath
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest, sourceSets.samples
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    useJUnitPlatform()
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

task samplesTest(type: Test) {
    description = 'Runs the sample builds.'
    group = 'verification'
    useJUnitPlatform()
    testClassesDirs = sourceSets.samples.output.classesDirs
    classpath = sourceSets.samples.runtimeClasspath
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/rodm/gradle-teamcity-plugin'
    vcsUrl = 'https://github.com/rodm/gradle-teamcity-plugin'
    description = 'Gradle plugin for developing TeamCity plugins'
    tags = ['teamcity']

    plugins {
        teamcityServerPlugin {
            id = 'com.github.rodm.teamcity-server'
            displayName = 'Gradle TeamCity Server plugin'
        }
        teamcityAgentPlugin {
            id = 'com.github.rodm.teamcity-agent'
            displayName = 'Gradle TeamCity Agent plugin'
        }
        teamcityCommonPlugin {
            id = 'com.github.rodm.teamcity-common'
            displayName = 'Gradle TeamCity Common API plugin'
        }
        teamcityEnvironmentsPlugin {
            id = 'com.github.rodm.teamcity-environments'
            displayName = 'Gradle TeamCity Environments plugin'
        }
    }
}
