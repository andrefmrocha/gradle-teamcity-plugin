
plugins {
    id 'org.gradle.groovy'
    id 'org.gradle.java-gradle-plugin'
    id 'org.gradle.jacoco'
    id 'org.gradle.maven-publish'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'org.sonarqube' version '2.8'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'de.undercouch:gradle-download-task:3.4.3'
    implementation 'org.jetbrains.intellij.plugins:structure-base:3.45'
    implementation 'org.jetbrains.intellij.plugins:structure-teamcity:3.45'
    implementation 'org.jetbrains.intellij:plugin-repository-rest-client:0.6.9'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.xmlunit:xmlunit-matchers:2.6.2'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:2.23.4'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '1.3-SNAPSHOT'
group = 'com.github.rodm'

sourceSets {
    functionalTest {
        compileClasspath += main.output + configurations.testRuntimeClasspath
        runtimeClasspath += main.output + configurations.testRuntimeClasspath
        groovy {
            srcDir 'src/functional/groovy'
        }
    }
    samples {
        compileClasspath += main.output + configurations.testRuntimeClasspath
        runtimeClasspath += main.output + configurations.testRuntimeClasspath
    }
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest, sourceSets.samples
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

task samplesTest(type: Test) {
    description = 'Runs the sample builds.'
    group = 'verification'
    testClassesDirs = sourceSets.samples.output.classesDirs
    classpath = sourceSets.samples.runtimeClasspath
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/rodm/gradle-teamcity-plugin'
    vcsUrl = 'https://github.com/rodm/gradle-teamcity-plugin'
    description = 'Gradle plugin for developing TeamCity plugins'
    tags = ['teamcity']

    plugins {
        teamcityServerPlugin {
            id = 'com.github.rodm.teamcity-server'
            displayName = 'Gradle TeamCity Server plugin'
        }
        teamcityAgentPlugin {
            id = 'com.github.rodm.teamcity-agent'
            displayName = 'Gradle TeamCity Agent plugin'
        }
        teamcityCommonPlugin {
            id = 'com.github.rodm.teamcity-common'
            displayName = 'Gradle TeamCity Common API plugin'
        }
        teamcityEnvironmentsPlugin {
            id = 'com.github.rodm.teamcity-environments'
            displayName = 'Gradle TeamCity Environments plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}
